plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.9.7.1'
}

tasks.wrapper {
    gradleVersion = '7.3'
    distributionType = Wrapper.DistributionType.ALL
}

group = 'org.lwjgl'
description = 'LWJGL3 demos with native image by GraalVM'
version = '3.3.1-SNAPSHOT'

ext {
    lwjglVersion = project.version
    jomlVersion = '1.10.3-SNAPSHOT'
    swtMavenVersion = '3.105.3'  //contains Eclipse SWT 4.6.2.9 software
    graalvmVersion = '21.3.0'

    //LWJGL modules used: minimal OpenGL, plus bgfx, cuda, jemalloc, par, shaderc, vma, vulkan
    lwjglModules = [
        'lwjgl', 'lwjgl-assimp', 'lwjgl-glfw', 'lwjgl-openal', 'lwjgl-opengl', 'lwjgl-stb',  //for OpenGL demos
        'lwjgl-bgfx',                                  //for BGFX demos
        'lwjgl-cuda',                                  //for CUDA demos (require NVIDIA hardware)
        'lwjgl-jemalloc', 'lwjgl-par',                 //utilities
        'lwjgl-shaderc', 'lwjgl-vma', 'lwjgl-vulkan',  //for Vulkan demos (require discrete GPU with Vulkan driver)
    ]

    mainClassName = 'org.lwjgl.demo.DemoLauncher'
    currentPlatform = getCurrentPlatform()
    nativeImageDirName = "native-image-$currentPlatform"
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenLocal()
}

//detect the OS (assuming 64-bit, on Intel/AMD hardware)
private static String getCurrentPlatform() {
    def currentOS = org.gradle.internal.os.OperatingSystem.current()
    return currentOS.isLinux() ? 'linux' :
           currentOS.isMacOsX() ? 'macos' :
           currentOS.isWindows() ? 'windows' :
           'unknown'
}

dependencies {
    implementation "org.joml:joml:$jomlVersion"
    //for compiling GraalVM substitution classes
    compileOnly "org.graalvm.nativeimage:svm:$graalvmVersion"

    //OS-specific SWT library (64-bit Intel/AMD hardware only)
    switch (currentPlatform) {
        case 'linux':
            implementation("org.eclipse.platform:org.eclipse.swt.gtk.linux.x86_64:$swtMavenVersion") {
                exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'
            }
            break
        case 'macos':
            implementation("org.eclipse.platform:org.eclipse.swt.cocoa.macosx.x86_64:$swtMavenVersion") {
                exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'
            }
            break
        case 'windows':
            implementation("org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:$swtMavenVersion") {
                exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'
            }
            break
        default:
            throw new GradleException('Unknown OS: ' + currentPlatform)
    }

    //get recommended dependency versions from the LWJGL BOM
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    //add LWJGL modules and their natives of current OS to the compile and runtime classpaths
    lwjglModules.each {
        implementation "org.lwjgl:$it"
        if (it != 'lwjgl-cuda' && it != 'lwjgl-egl') { //cuda, egl have no native modules
            //use natives for 64-bit Intel/AMD platforms only
            if (it == 'lwjgl-vulkan') {
                if (currentPlatform == 'macos') {
                    //Vulkan natives exist for Mac OSX only
                    runtimeOnly "org.lwjgl:$it::natives-$currentPlatform"
                }
            } else {
                runtimeOnly "org.lwjgl:$it::natives-$currentPlatform"
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['res', "graal-cfg/$currentPlatform"]
        }
    }
}

application {
    mainClass = project.mainClassName
    applicationName = project.name  //name of the resulting native executable
}

compileJava {
    options.release = 11  //use JDK11+ for compiling & running
    options.encoding = 'UTF-8'
}

run {
    //default demo to be run is bgfx.Bump
    //(the package "org.lwjgl.demo." is prepended automatically)
    args 'bgfx.Bump'

    //get system properties specified from the command line (for debugging, etc.)
    //and pass them on to the running application's JVM
    systemProperties = System.getProperties()

    //use the following jvmArgs for as many different run scenarios as possible,
    //and for all the code-execution paths as much as possible,
    //to generate (or merge with) the GraalVM native-image configuration files
    //in the graal-cfg/$currentPlatform/META-INF/native-image directory.
    //This directory is read by GraalVM during the native-image build.

    //jvmArgs = ["-agentlib:native-image-agent=config-merge-dir=graal-cfg/$currentPlatform/META-INF/native-image"]
}

task extractLwjglManifest(type: Copy) {
    from {
        configurations.compileClasspath.filter { it.name == "lwjgl-${lwjglVersion}.jar" }
                                       .collect { zipTree(it) }
    }
    include 'META-INF/MANIFEST.MF'
    into "$buildDir/tmp"
}

//create a stand-alone executable uber jar
//including all dependencies for the current platform
task uberJar(type: Jar) {
    dependsOn extractLwjglManifest
    archiveClassifier = "no-deps-$currentPlatform"

    with jar
    manifest {
        from (extractLwjglManifest.destinationDir.path + '/META-INF/MANIFEST.MF') {
            eachEntry { details -> 
                if (details.key == 'Created-By')
                    details.value = System.getProperty('java.runtime.version') + ' (' + System.getProperty('java.vendor') + ')'
                else if (details.key == 'Ant-Version')
                    details.exclude()
            }
        }
        attributes(
            'Main-Class': project.mainClassName,
            'Built-By': System.getProperty('user.name'),
            'Gradle-Version': 'Gradle ' + gradle.getGradleVersion(),
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    duplicatesStrategy 'exclude'
}

//remove unneeded .dll/.txt files
task removeUnneededDllFiles {
    doLast {
        ant.move(todir: "$buildDir/tmp/$nativeImageDirName") {
            fileset(dir: "$buildDir/$nativeImageDirName") {
                include name: '*.dll'
                include name: '*.txt'
            }
        }
        ant.move(file: "$buildDir/$nativeImageDirName/reports",
                 tofile: "$buildDir/tmp/$nativeImageDirName/reports")
        if (file("$buildDir/$nativeImageDirName/sources").exists()) {
            ant.move(file: "$buildDir/$nativeImageDirName/sources",
                     tofile: "$buildDir/tmp/$nativeImageDirName/sources")
        }
    }
}

graalvmNative {
    binaries {
        main {
            imageName = project.name
            mainClass = project.mainClassName
            verbose = true
            fallback = false

            buildArgs.add('--initialize-at-run-time=org.lwjgl')
            buildArgs.add('--native-image-info')
            buildArgs.add("-H:Path=$buildDir/$nativeImageDirName")
            buildArgs.add("-H:TempDirectory=$buildDir/tmp/$nativeImageDirName")

            useFatJar = false
        }
    }
}

tasks.named("nativeCompile") {
    //use the uber jar when long classpath becomes a problem in Windows:
    //dependsOn uberJar
    //classpathJar = uberJar.archiveFile
    finalizedBy removeUnneededDllFiles
}

